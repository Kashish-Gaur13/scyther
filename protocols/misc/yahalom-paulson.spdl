/*
 * Yahalom Paulson-strengthened
 * As in Sjouke's list
 */

const Alice,Bob,Simon,Eve : Agent;
secret k : Function;


protocol yahalompaulson(I,R,S)
{
	role I
	{
		fresh ni: Nonce;
		var nr: Nonce;
		var kir: SessionKey;
		var T: Ticket;

		send_1(I,R, I,ni);
		read_3(S,I, nr, {R,kir,ni}k(I,S), T );
                claim(I, SID, ni,nr);
		send_4(I,R, T, {nr}kir );

		claim_8(I, Secret,kir);
		claim_9(I, Nisynch);
		claim_10(I, Niagree);
	}

	role R
	{
		fresh nr: Nonce;
		var ni: Nonce;
		var kir: SessionKey;

		read_1(I,R, I,ni);
                claim(R, SID, ni,nr);
		send_2(R,S, R,nr,{I,ni}k(R,S) );
		read_4(I,R, {I,R,kir,nr}k(R,S), {nr}kir );

		claim_11(R, Secret,kir);
		claim_12(R, Nisynch);
		claim_13(R, Niagree);
	}

	role S
	{
		fresh kir: SessionKey;
		var ni,nr: Nonce;

		read_2(R,S, R,nr, {I,ni}k(R,S) );
                claim(S, SID, ni,nr);
		send_3(S,I, nr, { R,kir,ni }k(I,S), {I,R,kir,nr}k(R,S) );
	}
}

