/* 
 * KEA+C
 *
 * From "Security Analysis of KEA Authenticated Key Exchange Protocol"
 * Lauter, Mityagin, 2006.
 */

// Hash functions
const h1,h2,g1,g2: Function;
secret unh1,unh2,ung1,ung2: Function;

inversekeys (h1,unh1);
inversekeys (h2,unh2);
inversekeys (g1,ung1);
inversekeys (g2,ung2);

usertype String;
const C0,C1: String;

/*
 * Hack to gain equivalence of public keys to g^private.
 */
protocol @publickeys(RA,RB,RC)
{
	role RA
	{
		send_!1(RA,RA, g1(sk(RA)) );
	}
	role RB
	{
		// Special one to go into DH g2
		var T: Ticket;

		recv_!2(RB,RB, g2(g1(T),sk(RB)) );
		send_!3(RB,RB, g2(pk(RB),T) );
	}
	role RC
	{
		// Special one to go into DH g2
		var T: Ticket;

		recv_!4(RC,RC, g2(pk(RC),T) );
		send_!5(RC,RC, g2(g1(T),sk(RC)) );
	}
}

/*
 * Hack to simulate g^ab = g^ba.
 * '@' prefix of protocol name denotes helper protocol, which is used by
 * Scyther for displaying, and such protocols are ignored in
 * auto-generation of protocol modifiers.
 */
protocol @exponentiation(RA)
{
	role RA
	{
		var alpha,beta, T1,T2: Ticket;

		recv_!1(RA,RA, g2(g1(T1),T2) );
		send_!2(RA,RA, g2(g1(T2),T1) );
	}
}

// The protocol description

protocol KEAplusC(I,R)
{
	role I
	{
		const x: Nonce;
		var beta: Ticket;

		send_Compromise(I,I, x);
		send_1(I,R, g1(x) );
		recv_!2(R,I, beta,h2(C0,C0,
		  g2(beta,sk(I)),
		  g2(pk(R),x),
		  I,R
		));
		send_!3(I,R, h2(C1,C0,
		  g2(beta,sk(I)),
		  g2(pk(R),x),
		  I,R
		));

		claim(I,SKR, h1(C1,
		  g2(beta,sk(I)),
		  g2(pk(R),x),
		  I,R
		));
	}	
	
	role R
	{
		const y: Nonce;
		var alpha: Ticket;

		recv_1(I,R, alpha );
		send_Compromise(R,R, y);
		send_!2(R,I, g1(y), h2(C0,C0,
		  g2(pk(I),y),
		  g2(alpha,sk(R)),
		  I,R
		));
		recv_!3(I,R, h2(C1,C0,
		  g2(pk(I),y),
		  g2(alpha,sk(R)),
		  I,R
		));

		claim(R,SKR, h1(C1,
		  g2(pk(I),y),
		  g2(alpha,sk(R)),
		  I,R
		));
	}
}

