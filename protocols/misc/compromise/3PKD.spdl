/* 
 * 3PKD protocol
 *
 * From thesis Raymond Choo
 */

usertype SessionKey;

// PKI infrastructure

secret hk: Function;

// Hash

const hash: Function;
secret unhash: Function;
inversekeys (hash,unhash);

// The protocol description

protocol 3PKD(I,R,S)
{
	role I
	{
		const ri: Nonce;
		var rr: Nonce;
		var sesk: SessionKey;

		send_1(I,R, ri );
		read_3(S,I, { sesk }k(I,S), hash(hk(I,S),I,R,ri,{ sesk }k(I,S)) );

		claim_I(I,SKR,sesk);
	}	
	
	role R
	{
		var ri: Nonce;
		const rr: Nonce;
		var sesk: SessionKey;

		read_1(I,R, ri );
		send_2(R,S, ri,rr );
		read_4(S,R, { sesk }k(R,S), hash(hk(R,S),I,R,rr,{ sesk }k(R,S)) );

		claim_R(R,SKR,sesk);
	}

	role S
	{
		var ri: Nonce;
		var rr: Nonce;
		const sesk: SessionKey;

		read_2(R,S, ri,rr );
		send_3(S,I, { sesk }k(I,S), hash(hk(I,S),I,R,ri,{ sesk }k(I,S)) );
		send_4(S,R, { sesk }k(R,S), hash(hk(R,S),I,R,rr,{ sesk }k(R,S)) );
	}
}

