/* 
 * CMQV two pass version
 *
 */

// Hash functions
const h1,h2,g1,g2,exp,KDF,LSB: Function;
secret unh1,unh2,ung1,ung2,unexp,unKDF,unLSB: Function;

inversekeys (h1,unh1);
inversekeys (h2,unh2);
inversekeys (g1,ung1);
inversekeys (g2,ung2);
inversekeys (exp,unexp);
inversekeys (KDF,unKDF);
inversekeys (LSB,unLSB);

// Addition, multiplication
// For now, simply hashes
const mult,add: Function;
secret unmult,unadd: Function;

inversekeys(mult,unmult);
inversekeys(add,unadd);

/*
 * Hack to simulate public knowledge of public keys.
 */
protocol @publickeys(PK)
{
	role PK
	{
		send_!1(PK,PK, g1(sk(PK)));
	}
}
		
/*
 * Hack to simulate g^ab = g^ba inside terms.
 * '@' prefix of protocol name denotes helper protocol, which is used by
 * Scyther for displaying, and such protocols are ignored in
 * auto-generation of protocol modifiers.
 */
protocol @exponentiation(RA,RB,RC)
{
	role RA
	{
		var X,Y, T1,T2: Ticket;

		recv_!1(RA,RA, h2(
		  g2(g1(X),Y),
		  T1, T2, RA,RB
		  ));
		send_!2(RA,RA, h2(
		  g2(g1(Y),X),
		  T1, T2, RA,RB
		  ));
	}
	role RB
	{
		var X,Y, T1,T2: Ticket;

		recv_!3(RB,RB, h2(
		  T1,
		  g2(g1(X),Y),
		  T2, RA,RB
		  ));
		send_!4(RB,RB, h2(
		  T1,
		  g2(g1(Y),X),
		  T2, RA,RB
		  ));
	}
	role RC
	{
		var X,Y, T1,T2: Ticket;

		recv_!5(RC,RC, h2(
		  T1, T2,
		  g2(g1(X),Y),
		  RA,RB
		  ));
		send_!6(RC,RC, h2(
		  T1, T2,
		  g2(g1(Y),X),
		  RA,RB
		  ));
	}
}

protocol @addmult(I,R)
{
	role I
	{
		var X,Y: Ticket;

		recv_!1(I,I, add(X,Y) );
		send_!2(I,I, add(Y,X) );
	}
	role R
	{
		var X,Y: Ticket;

		recv_!3(R,R, mult(X,Y) );
		send_!4(R,R, mult(Y,X) );
	}
}

protocol @keyswap(I,R)
{
	role I
	{
		var x,y: Nonce;
		var hx,hy: Ticket;
		recv_!1(I,I, KDF(exp(mult(g1(hy),exp(g1(sk(R)),LSB(g1(hy),I,R))),add(hx,mult(LSB(g1(hx),I,R),sk(I)))),g1(hx),g1(hy),I,R) );
		send_!2(I,I, KDF(exp(mult(g1(hx),exp(g1(sk(I)),LSB(g1(hx),I,R))),add(hy,mult(LSB(g1(hy),I,R),sk(R)))),g1(hx),g1(hy),I,R) );
	}
}

// The protocol description

protocol CMQV-twopass(I,R)
{
	role I
	{
		const x: Nonce;
		var Y: Ticket;

		send_1(I,R, g1(h1(x,sk(I))) );
		recv_2(R,I, Y );

		claim(I,SKR,    KDF(exp(mult(Y,exp(g1(sk(R)),LSB(Y,I,R))),add(h1(x,sk(I)),mult(LSB(g1(h1(x,sk(I))),I,R),sk(I)))),g1(h1(x,sk(I))),Y,I,R) );
		claim(I,Secret, KDF(exp(mult(Y,exp(g1(sk(R)),LSB(Y,I,R))),add(h1(x,sk(I)),mult(LSB(g1(h1(x,sk(I))),I,R),sk(I)))),g1(h1(x,sk(I))),Y,I,R) );
	}	
	
	role R
	{
		const y: Nonce;
		var X: Ticket;

		recv_1(I,R, X );
		send_2(R,I, g1(h1(y,sk(R))) );

		claim(R,SKR,    KDF(exp(mult(X,exp(g1(sk(I)),LSB(X,I,R))),add(h1(y,sk(R)),mult(LSB(g1(h1(y,sk(R))),I,R),sk(R)))),X,g1(h1(y,sk(R))),I,R) );
		claim(R,Secret, KDF(exp(mult(X,exp(g1(sk(I)),LSB(X,I,R))),add(h1(y,sk(R)),mult(LSB(g1(h1(y,sk(R))),I,R),sk(R)))),X,g1(h1(y,sk(R))),I,R) );
	}
}

