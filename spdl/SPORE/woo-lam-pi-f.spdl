# Woo and Lam Pi f
#
# Modelled after the description in the SPORE library
# http://www.lsv.ens-cachan.fr/spore/wooLamPif.html
#
# Note:
# This protocol uses a ticket so scyther will only be able to verify
# the protocol using the ARACHNE engine (-a)
#

secret k: Function;

protocol woolamPif(I,R,S)
{
    role I
    {
        var Nr: Nonce;

        send_1(I,R, I); 
        read_2(R,I, Nr);
        send_3(I,R, {I,R,Nr}k(I,S));

    }    
    
    role R
    {
        const Nr: Nonce;
        var T: Ticket;

        read_1(I,R, I);
        send_2(R,I, Nr);
        read_3(I,R, T);
        send_4(R,S, {I, R, Nr, T}k(R,S)); 
        read_5(S,R, {I, R, Nr}k(R,S));
        
        claim_R1(R,Nisynch);
    }

    role S
    {
        var Nr: Nonce;
        
        read_4(R,S, {I, R, Nr,{I,R,Nr}k(I,S)}k(R,S));
        send_5(S,R, {I, R, Nr}k(R,S));
    }
}

const Alice,Bob,Eve,Simon: Agent;
const Te: Ticket;
const Ne: Nonce;


untrusted Eve;
compromised k(Eve,Simon);

run woolamPif.I (Agent,Agent,Simon);
run woolamPif.R (Agent,Agent,Simon);
run woolamPif.S (Agent,Agent,Simon);

