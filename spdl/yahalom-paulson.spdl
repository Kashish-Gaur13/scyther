/*
 * Yahalom Paulson-strengthened
 * As in Sjouke's list
 */

usertype Sessionkey, Ticket;

const Alice,Bob,Simon,Eve : Agent;
secret k : Function;

untrusted Eve;
compromised k(Eve,Simon);
const ne: Nonce;
const kee: Sessionkey;

protocol yahalomlowe(I,R,S)
{
	role I
	{
		const ni: Nonce;
		var nr: Nonce;
		var kir: Sessionkey;
		var T: Ticket;

		send_1(I,R, I,ni);
		read_3(S,I, nr, {R,kir,ni}k(I,S), T );
		send_4(I,R, T, {nr}kir );

		claim_8(I, Secret,kir);
		claim_9(I, Nisynch);
	}

	role R
	{
		const nr: Nonce;
		var ni: Nonce;
		var kir: Sessionkey;

		read_1(I,R, I,ni);
		send_2(R,S, R,nr,{I,ni}k(R,S) );
		read_4(I,R, {I,R,kir,nr}k(R,S), {nr}kir );

		claim_10(R, Secret,kir);
		claim_11(R, Nisynch);
	}

	role S
	{
		const kir: Sessionkey;
		var ni,nr: Nonce;

		read_2(R,S, R,nr, {I,ni}k(R,S) );
		send_3(S,I, nr, { R,kir,ni }k(I,S), {I,R,kir,nr}k(R,S) );
	}
}

run yahalomlowe.I(Agent,Agent,Simon);
run yahalomlowe.R(Agent,Agent,Simon);
run yahalomlowe.S(Agent,Agent,Simon);

run yahalomlowe.I(Agent,Agent,Simon);
run yahalomlowe.R(Agent,Agent,Simon);

