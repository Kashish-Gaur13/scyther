/* 
 * KEA+C
 *
 * From "Security Analysis of KEA Authenticated Key Exchange Protocol"
 * Lauter, Mityagin, 2006.
 */

// Hash functions
const h1,h2,gexp,gmult: Function;
secret unh1,unh2,ung1,ung2: Function;

inversekeys (h1,unh1);
inversekeys (h2,unh2);
inversekeys (gexp,ung1);
inversekeys (gmult,ung2);

usertype String;
const C0,C1: String;

/*
 * Hack to gain equivalence of public keys to g^private.
 */
protocol @publickeys(RA,RB,RC)
{
	role RA
	{
		send_!1(RA,RA, gexp(sk(RA)) );
	}
	role RB
	{
		// Special one to go into DH gmult
		var T: Ticket;

		recv_!2(RB,RB, gmult(gexp(T),sk(RB)) );
		send_!3(RB,RB, gmult(pk(RB),T) );
	}
	role RC
	{
		// Special one to go into DH gmult
		var T: Ticket;

		recv_!4(RC,RC, gmult(pk(RC),T) );
		send_!5(RC,RC, gmult(gexp(T),sk(RC)) );
	}
}

/*
 * Hack to simulate g^ab = g^ba.
 * '@' prefix of protocol name denotes helper protocol, which is used by
 * Scyther for displaying, and such protocols are ignored in
 * auto-generation of protocol modifiers.
 */
protocol @exponentiation(RA)
{
	role RA
	{
		var alpha,beta, T1,T2: Ticket;

		recv_!1(RA,RA, gmult(gexp(T1),T2) );
		send_!2(RA,RA, gmult(gexp(T2),T1) );
	}
}

// The protocol description

protocol KEAplusC(I,R)
{
	role I
	{
		fresh x: Nonce;
		var beta: Ticket;

		send_Compromise(I,I, x);
		send_1(I,R, gexp(x) );
		recv_!2(R,I, beta,h2(C0,C0,
		  gmult(beta,sk(I)),
		  gmult(gexp(sk(R)),x),
		  I,R
		));
		send_!3(I,R, h2(C1,C0,
		  gmult(beta,sk(I)),
		  gmult(pk(R),x),
		  I,R
		));

		claim(I,SKR, h1(C1,
		  gmult(beta,sk(I)),
		  gmult(gexp(sk(R)),x),
		  I,R
		));
	}	
	
	role R
	{
		fresh y: Nonce;
		var alpha: Ticket;

		recv_1(I,R, alpha );
		send_Compromise(R,R, y);
		send_!2(R,I, gexp(y), h2(C0,C0,
		  gmult(pk(I),y),
		  gmult(alpha,sk(R)),
		  I,R
		));
		recv_!3(I,R, h2(C1,C0,
		  gmult(gexp(sk(I)),y),
		  gmult(alpha,sk(R)),
		  I,R
		));

		claim(R,SKR, h1(C1,
		  gmult(gexp(sk(I)),y),
		  gmult(alpha,sk(R)),
		  I,R
		));
	}
}

